apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

// for Bintray
def projectVersionCodeNr = Integer.parseInt(projectVersionCode)
def libGit = libGit
def libUrl = libUrl
def libDescription = libDescription
def libGroupId = libGroupId
def libArtifactId = libArtifactId

// for Maven
group = libGroupId
def projectVersionName = projectVersionName

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode = projectVersionCodeNr
        versionName = projectVersionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Support library
    implementation "com.android.support:appcompat-v7:$supportLibVersion"

    // Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Lifecycle architecture components
    def lifecycle_version = "1.1.1"
    implementation "android.arch.lifecycle:viewmodel:$lifecycle_version"

    // Java inject API for dependency injection
    api 'javax.inject:javax.inject:1'
}

repositories {
    mavenCentral()
    google()
}

apply plugin: 'com.github.dcendents.android-maven'

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom.project {
            packaging 'aar'
            groupId libGroupId
            artifactId libArtifactId

            // Add your description here
            name libArtifactId
            description libDescription
            url libUrl

            // Set your license
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url libLicenseUrl
                }
            }
            scm {
                connection libGit
                developerConnection libGit
                url libUrl
            }
        }

    }
}

apply plugin: 'com.jfrog.bintray'

version = projectVersionName

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

artifacts {
    archives sourcesJar
}

// Bintray
bintray {
    user = private_bintray_user
    key = private_bintray_apikey

    configurations = ['archives']
    pkg {
        repo = libGroupId
        name = libArtifactId
        desc = libDescription
        websiteUrl = libUrl
        vcsUrl = libGit
        licenses = ["Apache-2.0"]
        publish = true
        publicDownloadNumbers = true
        version {
            name = projectVersionName
            desc = libDescription
            gpg {
                sign = true
                passphrase = private_bintray_gpg_passphrase
            }
        }
    }
}
